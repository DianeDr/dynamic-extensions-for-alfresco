allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: "maven-publish"

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
        repositories {
            maven {
                url 'https://api.bintray.com/maven/laurentvanderlinden/maven/dynamic-extensions-alfresco'
                credentials {
                    username = project.hasProperty('bintray_username') ? project.bintray_username : ''
                    password = project.hasProperty('bintray_api_key') ? project.bintray_api_key : ''
                }
            }
        }
    }

    [compileJava, compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

    compileJava {
        sourceCompatibility = 1.6
        targetCompatibility = 1.6
        options.compilerArgs << '-Xlint:unchecked'
    }

    ext {
        alfrescoVersion = '5.0.b'
        springVersion = '3.2.10.RELEASE'
        geminiVersion = '1.0.2.RELEASE'
        slf4jVersion = '1.7.5'
        osgiVersion = '4.3.1'
        junitVersion = '4.8.2'
        transactionApiVersion = '1.1'
        felixVersion = '4.2.1'
        springOsgiUtilVersion = '2.0.5.RELEASE'
        mockitoVersion = '1.9.5'
        jibxVersion = '1.2.3'
        hibernateVersion = '3.2.6.ga'
        servletVersion = '2.5'
    }

    repositories {
        mavenCentral()
        maven {
            url "https://artifacts.alfresco.com/nexus/content/groups/public/"
        }
        jcenter()
    }

    group = 'com.github.dynamicextensionsalfresco'
    version = '1.1.3'

    configurations.all {
        exclude group: 'jaxen'
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'org.springframework') {
                    details.useVersion project.ext.springVersion
                }
            }
        }
    }

    configurations {
        amp
        compile.extendsFrom(amp)
    }
}

defaultTasks 'build'
